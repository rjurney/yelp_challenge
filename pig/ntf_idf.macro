/* A Pig Macro to implement the NTF-IDF Algorithm */
DEFINE ntf_idf(token_records, id_field, id_field_2, token_field) RETURNS out_relation {

  /* Calculate the term count per document */
  doc_word_totals = foreach (group $token_records by ($id_field, $id_field_2, $token_field)) 
  generate 
    FLATTEN(group) AS ($id_field, $id_field_2, $token_field), 
    COUNT_STAR($token_records) AS doc_total;

  /* Calculate the document size */
  pre_term_counts = foreach (group doc_word_totals by ($id_field, $id_field_2)) generate
    FLATTEN(group) AS ($id_field, $id_field_2),
    FLATTEN(doc_word_totals.($token_field, doc_total)) AS ($token_field, doc_total), 
    SUM(doc_word_totals.doc_total) AS doc_size,
    MAX(doc_word_totals.doc_total) AS max_freq;

  /* Calculate the TF - Term Frequency */
  term_freqs = foreach pre_term_counts generate 
    $id_field AS $id_field,
    $id_field_2 AS $id_field_2,
    $token_field AS $token_field,
    ((double)doc_total / (double)doc_size / (double) max_freq) AS term_freq;

  /* Get count of documents using each token, for idf */
  token_usages = foreach (group term_freqs by $token_field) generate
    FLATTEN(term_freqs) as ($id_field:chararray, $id_field_2:chararray, $token_field:chararray, 
    term_freq:double),
    COUNT_STAR(term_freqs) as num_docs_with_token;

  /* Get document count */
  just_ids = foreach $token_records generate $id_field, $id_field_2;
  just_ids = DISTINCT just_ids;
  ndocs = foreach (group just_ids all) generate COUNT_STAR(just_ids) 
  as total_docs;

  /* Note the use of Pig Scalars to calculate idf */
  scores = foreach token_usages {
    idf    = LOG((double)ndocs.total_docs/(double)num_docs_with_token);
    ntf_idf = (double)term_freq * idf;
    generate $id_field as $id_field,
             $id_field_2 AS $id_field_2,
             $token_field as $token_field,
             (double)ntf_idf as score:double;
  };

  $out_relation = filter scores by $token_field IS NOT NULL and $token_field != '' and LENGTH($token_field) > 2; -- score > 0.10 and
};